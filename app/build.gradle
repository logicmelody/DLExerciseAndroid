apply plugin: 'com.android.application'

def final defaultApplicationId = "com.dl.dlexerciseandroid"

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    // 如果要使用productFlavor的功能，Android plugin 3.0.0之後需要declare flavorDimensions
    flavorDimensions "default"

    defaultConfig {
        applicationId defaultApplicationId
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true

        // Default test runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // For Ndk CMake
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }

        ndk {
            abiFilters "armeabi-v7a"
            abiFilters "x86"
        }

        // 如果在 Variant 裡面沒有定義的話，就會使用 defaultConfig 裡面的值，所以記得在 defaultConfig 也要新增！
        resValue "string", "app_name", "DLExercise"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "logicmelody.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {

        }

        release {
            // 打開Proguard功能
            minifyEnabled false

            zipAlignEnabled true
            signingConfig signingConfigs.release

            // getDefaultProguardFile('proguard-android.txt'):
            // Gets the default ProGuard settings from the Android SDK tools/proguard/ folder
            // proguard-rules.pro:
            // 我們自己可以customize的Proguard rule
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            applicationId = defaultApplicationId + ".dev"

            // 會在產出的 resource 裡面新增一組 R.string.app_name 然後內容是這邊設定的值
            resValue "string", "app_name", "DLExercise Dev"

            // 在BuildConfig中會多出一個static的變數，我們可以利用這個變數來決定要不要顯示用來測試的功能
            buildConfigField "boolean", "IS_DEV", "true"

            manifestPlaceholders = [
                    application_id: defaultApplicationId + ".dev",
                    app_icon: "@mipmap/ic_launcher_dev"
            ]
        }

        prod {
            applicationId = defaultApplicationId
            buildConfigField "boolean", "IS_DEV", "false"
            resValue "string", "app_name", "DLExercise"

            manifestPlaceholders = [
                    application_id: defaultApplicationId,
                    app_icon: "@mipmap/ic_launcher"
            ]
        }
    }

    // For consuming restful web service with Spring
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    // For Ndk CMake
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    dexOptions {
        jumboMode true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':dlsdk')
    // For consuming restful web service with Spring
    implementation 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    // For Facebook
    implementation 'com.facebook.android:facebook-android-sdk:4.29.0'
    // For using Firebase，版本號碼必須跟Google Play Services一樣
    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation 'com.google.firebase:firebase-messaging:15.0.2'
    // For OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    // For RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex.rxjava2:rxjava:2.1.11'
    // For Retrofit (版本要一致)
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    // JSON Parsing: Gson
    implementation 'com.google.code.gson:gson:2.8.2'
    // ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.6.5'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.github.danylovolokh:list-visibility-utils:0.2.0'
    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.7.1'
    // Required -- JUnit 4 framework, for 一般的unit tests
    testImplementation 'junit:junit:4.12'
    // Optional -- Mockito framework, for 跟Android framework有一點相依性的unit tests
    testImplementation 'org.mockito:mockito-core:2.10.0'
    // Optional -- Hamcrest library, which lets you create more flexible assertions using the Hamcrest matcher APIs
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso，用來作Android UI測試
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Optional -- UI testing with UI Automator，vXX的版本必須要跟minSdkVersion一致(e.g. 這邊都是18)
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

// For using Firebase: 這行要加在最後一行
apply plugin: 'com.google.gms.google-services'

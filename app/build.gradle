apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.dl.dlexerciseandroid"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        // Default test runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // For Ndk CMake
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }

        ndk {
            abiFilters "armeabi"
            abiFilters "x86"
        }
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "logicmelody.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            // 打開Proguard功能
            minifyEnabled false

            zipAlignEnabled true
            signingConfig signingConfigs.release

            // getDefaultProguardFile('proguard-android.txt'):
            // Gets the default ProGuard settings from the Android SDK tools/proguard/ folder
            // proguard-rules.pro:
            // 我們自己可以customize的Proguard rule
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 我們還可以針對不同的product版本定義不同的proguard rule
    // Ex: flavor2就接受了三種不同的proguard rules(proguard-android.txt, proguard-rules.pro, flavor2-rules.pro)
//    productFlavors {
//        flavor1 {
//        }
//        flavor2 {
//            proguardFile 'flavor2-rules.pro'
//        }
//    }

    // For consuming restful web service with Spring
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    // For Ndk CMake
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    dexOptions {
        jumboMode true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':dlsdk')

    // For consuming restful web service with Spring
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'

    // For Facebook
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'

    // For Google Play Services，版本號碼必須跟Firebase一樣
    compile 'com.google.android.gms:play-services-analytics:10.0.1'

    // For using Firebase，版本號碼必須跟Google Play Services一樣
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'

    // For OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    // For RxJava
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'

    // For Retrofit (版本要一致)
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    // JSON Parsing: Gson
    compile 'com.google.code.gson:gson:2.8.2'

    // ButterKnife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // EventBus
    compile 'org.greenrobot:eventbus:3.1.1'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.github.danylovolokh:list-visibility-utils:0.2.0'

    // Required -- JUnit 4 framework, for 一般的unit tests
    testCompile 'junit:junit:4.12'

    // Optional -- Mockito framework, for 跟Android framework有一點相依性的unit tests
    testCompile 'org.mockito:mockito-core:1.10.19'

    // Optional -- Hamcrest library, which lets you create more flexible assertions using the Hamcrest matcher APIs
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'

    // Optional -- UI testing with Espresso，用來作Android UI測試
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Optional -- UI testing with UI Automator，vXX的版本必須要跟minSdkVersion一致(e.g. 這邊都是18)
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

// For using Firebase: 這行要加在最後一行
apply plugin: 'com.google.gms.google-services'
